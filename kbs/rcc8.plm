;;; -*- Mode: Lisp; Package: STELLA; Syntax: COMMON-LISP; Base: 10 -*-

;;; File implementing the RCC8 (Region Connection Calculus)
;;; Author: University of Southern California
;;; Version: rcc8.plm,v 1.3 2009/01/15 01:55:05 tar Exp

;;;;;;;;;;;;;;;;;;;;;;;;;;;; BEGIN LICENSE BLOCK ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;                                                                            ;
; Version: MPL 1.1/GPL 2.0/LGPL 2.1                                          ;
;                                                                            ;
; The contents of this file are subject to the Mozilla Public License        ;
; Version 1.1 (the "License"); you may not use this file except in           ;
; compliance with the License. You may obtain a copy of the License at       ;
; http://www.mozilla.org/MPL/                                                ;
;                                                                            ;
; Software distributed under the License is distributed on an "AS IS" basis, ;
; WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License   ;
; for the specific language governing rights and limitations under the       ;
; License.                                                                   ;
;                                                                            ;
; The Original Code is the PowerLoom KR&R System.                            ;
;                                                                            ;
; The Initial Developer of the Original Code is                              ;
; UNIVERSITY OF SOUTHERN CALIFORNIA, INFORMATION SCIENCES INSTITUTE          ;
; 4676 Admiralty Way, Marina Del Rey, California 90292, U.S.A.               ;
;                                                                            ;
; Portions created by the Initial Developer are Copyright (C) 2008           ;
; the Initial Developer. All Rights Reserved.                                ;
;                                                                            ;
; Contributor(s):                                                            ;
;                                                                            ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;; END LICENSE BLOCK ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;;
;;;;


(defmodule "RCC8"
  :documentation "Module for the Region Connection Calculus 8 (RCC8) relations.  1.3

Implements DC, EC, PO, TPP, NTPP, TPPI, NTPPI relations directly.
For EQ the standard PowerLoom equality (=) is used.  That means
that if equality is desired, one must create the regions using
the REGION function, so that they end up being skolems.

Examples:
 (assert (dc (region g0) (region g1)))
 (assert (ntpp (region g1) (region g2)))
 (assert (ntpp (region g2) (region g3)))
 (assert (= (region g3) (region g3alt)))

 (retrieve all (ntpp (region ?x) (region ?y)))
 (retrieve all (ntppi (region ?x) (region ?y)))
 (retrieve all (dc (region ?x) (region ?y)))
 (retrieve all (not (dc (region ?x) (region ?y))))

For more background on the RCC8, consult
  David A. Randell and Zhan Cui and Anthony Cohn,
  'A Spatial Logic Based on Regions and Connection',
  KR '92. Principles of Knowledge Representation and Reasoning:
          Proceedings of the Third International Conference,
  pp. 165-176, 1992.

  Anthony G. Cohn, Brandon Bennett, John Gooday and Nicholas M. Gotts,
  'Representing and Reasoning with Qualitative Spatial Relations About Regions',
  in Spatial and Temporal Reasoning, Oliviero Stock (ed).,
  (Kluwer Academic Publishers: Boston), 1997.
"
  :includes ("PL-KERNEL-KB"))

(in-module "RCC8")

(defconcept geometric-region)
(defconcept geometric-region-2d (geometric-region))
(defconcept geometric-region-3d (geometric-region))
(deffunction region ((?x THING)) :-> (?g GEOMETRIC-REGION))

(defrelation dc ((?x geometric-region) (?y geometric-region))
  :documentation "DC: Disconnected regions"
  :axioms (symmetric dc)
  :=> (and (not (ec ?x ?y))
	   (not (po ?x ?y))
	   (not (tpp ?x ?y))
	   (not (ntpp ?x ?y))
	   (not (tppi ?x ?y))
	   (not (ntppi ?x ?y))
	   (not (= ?x ?y)))
  )

(defrelation ec ((?x geometric-region) (?y geometric-region))
  :documentation "EC: Externally Connected regions"
  :axioms (symmetric ec)
  :=> (and (not (dc ?x ?y))
	   (not (po ?x ?y))
	   (not (tpp ?x ?y))
	   (not (ntpp ?x ?y))
	   (not (tppi ?x ?y))
	   (not (ntppi ?x ?y))
	   (not (= ?x ?y)))
  )

(defrelation po ((?x geometric-region) (?y geometric-region))
  :documentation "PO: Partially Overlapping regions"
  :axioms (symmetric po)
  :=> (and (not (dc ?x ?y))
	   (not (ec ?x ?y))
	   (not (tpp ?x ?y))
	   (not (ntpp ?x ?y))
	   (not (tppi ?x ?y))
	   (not (ntppi ?x ?y))
	   (not (= ?x ?y)))
  )

(defrelation tpp ((?x geometric-region) (?y geometric-region))
  :documentation "TPP: Tangential Proper Part"
  :=> (and (not (dc ?x ?y))
	   (not (ec ?x ?y))
	   (not (po ?x ?y))
	   (not (ntpp ?x ?y))
	   (not (tppi ?x ?y))
	   (not (ntppi ?x ?y))
	   (not (= ?x ?y)))
  )

(defrelation ntpp ((?x geometric-region) (?y geometric-region))
  :documentation "NTPP: Non-Tangential Proper Part"
  :=> (and (not (dc ?x ?y))
	   (not (ec ?x ?y))
	   (not (po ?x ?y))
	   (not (tpp ?x ?y))
	   (not (tppi ?x ?y))
	   (not (ntppi ?x ?y))
	   (not (= ?x ?y)))
  )


(defrelation tppi ((?x geometric-region) (?y geometric-region))
  :documentation "TPPI: Tangential Proper Part Inverse"
  :<=> (tpp ?y ?x)  ; INVERSE, but want it for backward chaining
  :inverse tpp
  :=> (and (not (dc ?x ?y))
	   (not (ec ?x ?y))
	   (not (po ?x ?y))
	   (not (tpp ?x ?y))
	   (not (ntpp ?x ?y))
	   (not (ntppi ?x ?y))
	   (not (= ?x ?y)))
  )

(defrelation ntppi ((?x geometric-region) (?y geometric-region))
  :documentation "NTPPI: Non-Tangential Proper Part Inverse"
  :<=> (ntpp ?y ?x)  ; INVERSE, but want it for backward chaining
  :inverse ntpp
  :=> (and (not (dc ?x ?y))
	   (not (ec ?x ?y))
	   (not (po ?x ?y))
	   (not (tpp ?x ?y))
	   (not (ntpp ?x ?y))
	   (not (tppi ?x ?y))
	   (not (= ?x ?y)))
  )

;; Coverage axiom.

#|  ;; Disable for now, since we don't do reasoning by cases.
(assert (=> (and (geometric-region ?x) (geometric-region ?y))
	    (or (dc ?x ?y)
		(ec ?x ?y)
		(po ?x ?y)
		(tpp ?x ?y)
		(ntpp ?x ?y)
		(tppi ?x ?y)
		(ntppi ?x ?y)
		(= ?x ?y))))
|#


;; Unambiguous composition rules.

(assert (=> (and (dc ?x ?y) (tpp ?y ?z))
	    (dc ?x ?z)))

(assert (=> (and (dc ?x ?y) (ntpp ?y ?z))
	    (dc ?x ?z)))

(assert (=> (and (ec ?x ?y) (ntpp ?y ?z))
	    (dc ?x ?z)))

(assert (=> (and (tpp ?x ?y) (ntpp ?y ?z))
	    (ntpp ?x ?z)))

(assert (=> (and (ntpp ?x ?y) (tpp ?y ?z))
	    (ntpp ?x ?z)))

(assert (=> (and (ntpp ?x ?y) (ntpp ?y ?z))
	    (ntpp ?x ?z)))

(assert (=> (and (tppi ?x ?y) (dc ?y ?z))
	    (dc ?x ?z)))

(assert (=> (and (tppi ?x ?y) (ntppi ?y ?z))
	    (ntppi ?x ?z)))

(assert (=> (and (ntppi ?x ?y) (dc ?y ?z))
	    (dc ?x ?z)))

(assert (=> (and (ntppi ?x ?y) (ec ?y ?z))
	    (dc ?x ?z)))

(assert (=> (and (ntppi ?x ?y) (tppi ?y ?z))
	    (ntppi ?x ?z)))

(assert (=> (and (ntppi ?x ?y) (ntppi ?y ?z))
	    (ntppi ?x ?z)))


#|
;;; TESTS

(assert (and (tpp (region g1) (region g2))
	     (tpp (region g2) (region g3))))


(ask (tppi (region g3) (region g2)))
; => true
(ask (dc (region g2) (region g3)))
; => false

(retrieve all (tpp ?x ?y))
;  ?x = g2, ?y = g3
;  ?x = g1, ?y = g3

(assert (dc (region g0) (region g1)))

(retrieve all (dc ?x ?y))
(retrieve all (dc (region ?x) (region ?y)))
(retrieve all (not (dc ?x ?y))) 

|#